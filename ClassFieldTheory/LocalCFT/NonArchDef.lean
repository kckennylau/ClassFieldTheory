import ClassFieldTheory.Mathlib.ValuativeLemmas
import Mathlib

/-!
# Definition of Non-Archimedean Local Fields

We define non-archimedean local fields as a class `IsNonArchLF`.

-/

universe u v

class IsNonarchLocalField (K : Type u) [Field K] [ValuativeRel K] [UniformSpace K] : Prop extends
  ValuativeTopology K,
  IsUniformAddGroup K,
  LocallyCompactSpace K,
  ValuativeRel.IsNontrivial K
  -- ValuativeRel.IsRankLeOne K -- TODO: in future mathlib
  -- IsTopologicalDivisionRing K,
  -- CompleteSpace K,
  -- ValuativeRel.IsDiscrete K

open ValuativeRel

namespace ValuativeExtension

variable (A : Type u) (B : Type v) [CommRing A] [CommRing B] [ValuativeRel A] [ValuativeRel B]
  [Algebra A B] [ValuativeExtension A B] (a b : A)

lemma algebraMap_le : valuation B (algebraMap A B a) ‚â§ valuation B (algebraMap A B b) ‚Üî
    valuation A a ‚â§ valuation A b := by
  simp_rw [‚Üê Valuation.Compatible.rel_iff_le, rel_iff_rel]

lemma algebraMap_eq : valuation B (algebraMap A B a) = valuation B (algebraMap A B b) ‚Üî
    valuation A a = valuation A b := by
  simp_rw [le_antisymm_iff, algebraMap_le]

lemma algebraMap_lt : valuation B (algebraMap A B a) < valuation B (algebraMap A B b) ‚Üî
    valuation A a < valuation A b := by
  simp_rw [lt_iff_le_not_ge, algebraMap_le]

end ValuativeExtension

namespace IsNonarchLocalField

section Padic

variable (p : ‚Ñï) [Fact p.Prime]

instance : LocallyCompactSpace ‚Ñö_[p] := inferInstance

instance : IsNonarchLocalField ‚Ñö_[p] where
  mem_nhds_iff := sorry

end Padic

variable (K : Type u) [Field K] [ValuativeRel K] [UniformSpace K] [IsNonarchLocalField K]
  (L : Type v) [Field L] [ValuativeRel L] [UniformSpace L] [IsNonarchLocalField L]

instance : (Valued.v : Valuation K (ValueGroupWithZero K)).IsNontrivial :=
  ValuativeRel.isNontrivial_iff_isNontrivial.mp inferInstance

instance : IsTopologicalDivisionRing K := inferInstance

instance : ValuativeRel.IsRankLeOne K := sorry

noncomputable
instance : (Valued.v : Valuation K (ValueGroupWithZero K)).RankOne where
  hom := IsRankLeOne.nonempty.some.emb
  strictMono' := IsRankLeOne.nonempty.some.strictMono

open scoped Valued in
instance : ProperSpace K := ProperSpace.of_nontriviallyNormedField_of_weaklyLocallyCompactSpace K

instance : IsDiscreteValuationRing ùí™[K] :=
  (Valued.integer.properSpace_iff_completeSpace_and_isDiscreteValuationRing_integer_and_finite_residueField.mp inferInstance).2.1

instance : (Valued.v : Valuation K (ValueGroupWithZero K)).IsNontrivial :=
  ValuativeRel.isNontrivial_iff_isNontrivial.mp inferInstance

noncomputable
instance : (Valued.v : Valuation K (ValueGroupWithZero K)).RankOne where
  hom := IsRankLeOne.nonempty.some.emb
  strictMono' := IsRankLeOne.nonempty.some.strictMono

open scoped Valued in
instance : ProperSpace K := ProperSpace.of_nontriviallyNormedField_of_weaklyLocallyCompactSpace K

open Valued.integer in
instance compactSpace_integer : CompactSpace ùí™[K] :=
  properSpace_iff_compactSpace_integer.mp inferInstance

open Valued.integer in
instance : CompleteSpace ùí™[K] :=
  (compactSpace_iff_completeSpace_and_isDiscreteValuationRing_and_finite_residueField.mp
    (compactSpace_integer K)).1

open Valued.integer in
instance : Finite ùìÄ[K] :=
  (properSpace_iff_completeSpace_and_isDiscreteValuationRing_integer_and_finite_residueField.mp
    inferInstance).2.2

theorem prime_ringChar : (ringChar ùìÄ[K]).Prime :=
  CharP.char_is_prime ùìÄ[K] _

open Valued.integer in
instance : CompleteSpace K :=
  (properSpace_iff_completeSpace_and_isDiscreteValuationRing_integer_and_finite_residueField.mp
    inferInstance).1

/-- This is how you show that there is a uniformiser (which in Mathlib is called `Irreducible`). -/
example : ‚àÉ œñ : ùí™[K], Irreducible œñ :=
  IsDiscreteValuationRing.exists_irreducible _

example : ‚àÄ œñ : ùí™[K], Irreducible œñ ‚Üí œñ ‚â† 0 :=
  fun _ h ‚Ü¶ h.ne_zero

lemma associated_iff_of_irreducible (x y : ùí™[K]) (hx : Irreducible x) :
    Associated y x ‚Üî Irreducible y :=
  ‚ü®fun hyx ‚Ü¶ hyx.symm.irreducible hx,
  fun hy ‚Ü¶ IsDiscreteValuationRing.associated_of_irreducible _ hy hx‚ü©

theorem open_OK : IsOpen (ùí™[K] : Set K) :=
  sorry

def compactOpenOK : TopologicalSpace.CompactOpens K where
  carrier := ùí™[K]
  isCompact' := isCompact_iff_compactSpace.mpr <| compactSpace_integer K
  isOpen' := open_OK K

-- TODO: add Haar measure (or check that it works with `example`)

variable [Algebra K L] [ValuativeExtension K L]

instance : FiniteDimensional K L :=
  sorry

omit [UniformSpace K] [IsNonarchLocalField K] [UniformSpace L] [IsNonarchLocalField L] in
lemma algebraMap_mem_integer (x : ùí™[K]) : (algebraMap ùí™[K] L) x ‚àà ùí™[L] := by
  rcases x with ‚ü®x, hx‚ü©
  change valuation L (algebraMap K L x) ‚â§ 1
  simpa only [map_one] using (ValuativeExtension.algebraMap_le K L x 1).mpr hx

instance : Algebra ùí™[K] ùí™[L] where
  smul r a := ‚ü®r ‚Ä¢ a, Algebra.smul_def r (a : L) ‚ñ∏ mul_mem (algebraMap_mem_integer ..) a.2‚ü©
  algebraMap := (algebraMap K L).restrict ùí™[K] ùí™[L] fun x hx => algebraMap_mem_integer K L ‚ü®x, hx‚ü©
  commutes' _ _ := Subtype.ext (Algebra.commutes _ _)
  smul_def' _ _ := Subtype.ext (Algebra.smul_def _ _)

namespace ValuativeRel

theorem posSubmonoid.ne_zero {R : Type u} [CommRing R] [ValuativeRel R]
    (x : posSubmonoid R) : x.val ‚â† 0 :=
  mt (¬∑ ‚ñ∏ rel_rfl) x.2

theorem valuation_surjective‚ÇÄ {F : Type u} [Field F] [ValuativeRel F]
    (Œ≥ : ValueGroupWithZero F) : ‚àÉ x : F, valuation F x = Œ≥ :=
  let ‚ü®x, y, hxy‚ü© := valuation_surjective Œ≥
  ‚ü®x / y.val, by rw [map_div‚ÇÄ, hxy]‚ü©

theorem units_map_valuation_surjective {F : Type u} [Field F] [ValuativeRel F]
    (Œ≥ : (ValueGroupWithZero F)À£) : ‚àÉ x : FÀ£, Units.map (valuation F) x = Œ≥ :=
  let ‚ü®x, hx‚ü© := valuation_surjective‚ÇÄ Œ≥.val
  ‚ü®Units.mk0 x (mt (by rw [‚Üê hx, ¬∑, map_zero]) Œ≥.ne_zero),
    Units.ext <| by simpa using hx‚ü©

end ValuativeRel

theorem density (y : LÀ£) : ‚àÉ (x : KÀ£), Valued.v (algebraMap K L x) ‚â§ Valued.v y.val := sorry

instance : ContinuousSMul K L := by
  apply continuousSMul_of_algebraMap K L (continuous_of_continuousAt_zero _ _)
  simp only [ContinuousAt, map_zero]
  obtain B‚ÇÅ := Valued.hasBasis_nhds_zero K (ValueGroupWithZero K)
  obtain B‚ÇÇ := Valued.hasBasis_nhds_zero L (ValueGroupWithZero L)
  apply (Filter.HasBasis.tendsto_iff B‚ÇÅ B‚ÇÇ).mpr
  simp only [Set.mem_setOf_eq, true_and]
  intro b hb
  obtain ‚ü®a, ha‚ü© := IsNonarchLocalField.ValuativeRel.units_map_valuation_surjective b
  rw [‚Üê ha]
  obtain ‚ü®a', ha'‚ü© := density K L a
  use Units.map (valuation K) (a')
  intro x hx
  simp only [Units.coe_map, MonoidHom.coe_coe] at *
  change valuation _ _ ‚â§ valuation _ _ at ha'
  change valuation _ _ < valuation _ _
  change valuation _ _ < valuation _ _  at hx
  exact lt_of_lt_of_le ((ValuativeExtension.algebraMap_lt K L x a'.val).mpr hx) ha'


-- TODO: Maddy

instance : Module.Finite ùí™[K] ùí™[L] :=
  sorry

@[fun_prop] lemma continuous_algebraMap : Continuous (algebraMap K L) :=
  _root_.continuous_algebraMap K L

instance : IsScalarTower ùí™[K] K L := inferInstance

instance : IsScalarTower ùí™[K] ùí™[L] L := sorry --inferInstance

noncomputable def e : ‚Ñï :=
  Ideal.ramificationIdx (algebraMap ùí™[K] ùí™[L]) ùìÇ[K] ùìÇ[L]

theorem e_spec {œñK : ùí™[K]} {œñL : ùí™[L]} (hk : Irreducible œñK) (hl : Irreducible œñL) :
    Associated (œñL ^ e K L) (algebraMap ùí™[K] ùí™[L] (œñK)) :=
  sorry

noncomputable def f : ‚Ñï :=
  Ideal.inertiaDeg ùìÇ[K] ùìÇ[L]

instance : ùìÇ[L].LiesOver ùìÇ[K] := sorry

theorem f_spec : Nat.card ùìÄ[K] ^ f K L = Nat.card ùìÄ[K] := sorry

lemma non_triv_maximal_embedding : (Ideal.map (algebraMap ùí™[K] ùí™[L]) ùìÇ[K]) ‚â† ‚ä•
  ‚àß (Ideal.map (algebraMap ùí™[K] ùí™[L]) ùìÇ[K]) ‚â† ‚ä§ := sorry

theorem e_pos : 0 < e K L := sorry

theorem f_pos : 0 < f K L := Ideal.inertiaDeg_pos ùìÇ[K] ùìÇ[L]

lemma irreducible_pow_span_pow {R : Type u} [CommRing R] [IsDomain R] [IsDiscreteValuationRing R]
  {œñ : R} (h : Irreducible œñ) {n : ‚Ñï} : Ideal.span {œñ ^ n} = (Ideal.span {œñ})^(n) := sorry

lemma unique_maximal_ideal_factorization [DecidableEq (Ideal ‚Ü•ùí™[L])] : (UniqueFactorizationMonoid.factors
  (Ideal.map (algebraMap ùí™[K] ùí™[L]) ùìÇ[K])).toFinset = {ùìÇ[L]} := by
  obtain ‚ü®œñ, hœñ‚ü© := IsDiscreteValuationRing.exists_irreducible ùí™[L]
  obtain ‚ü®n, hn‚ü© := IsDiscreteValuationRing.ideal_eq_span_pow_irreducible (non_triv_maximal_embedding K L).1 hœñ
  have irred_ele_span_irred_ideal : Irreducible (Ideal.span {œñ}) := by
    sorry
  rw [hn, Irreducible.maximalIdeal_eq hœñ, irreducible_pow_span_pow hœñ]
  simp
  rw [UniqueFactorizationMonoid.normalizedFactors_irreducible irred_ele_span_irred_ideal]
  simp
  rw [Multiset.nsmul_singleton, Multiset.toFinset_replicate]
  have g : n ‚â† 0 := by
    intro h
    rw [h, irreducible_pow_span_pow] at hn
    simp at hn
    exact False.elim ((non_triv_maximal_embedding K L).2 hn)
    assumption
  simp
  intro a
  exact False.elim (g a)

theorem e_mul_f_eq_n : e K L * f K L = Module.finrank K L := by
  symm
  calc
  _ = (Ideal.ramificationIdx (algebraMap ùí™[K] ùí™[L]) ùìÇ[K] ùìÇ[L]) * (Ideal.inertiaDeg ùìÇ[K] ùìÇ[L]) := by
    symm
    rw [‚Üê Ideal.sum_ramification_inertia ùí™[L] ùìÇ[K]]
    classical rw [unique_maximal_ideal_factorization]
    rfl
    exact IsDiscreteValuationRing.not_a_field ‚Ü•ùí™[K]

-- TODO: generalise to extensions of DVRs.
class IsUnramified : Prop where
  e_eq_one : e K L = 1

theorem unramified_def : IsUnramified K L ‚Üî e K L = 1 :=
  ‚ü®fun h ‚Ü¶ h.1, fun h ‚Ü¶ ‚ü®h‚ü©‚ü©

theorem unramified_iff_unramified : IsUnramified K L ‚Üî Algebra.Unramified ùí™[K] ùí™[L] :=
  sorry

section make_finite_extension

/- # Constructing a finite extension from a minimal set of assumptions. -/

variable (L : Type v) [Field L] [Algebra K L] [FiniteDimensional K L]

/-
open scoped Valued in
#check (inferInstance : NormedField K)
#check (inferInstance : Valuation.RankOne (Valued.v (R := K)))
-/

open scoped Valued NormedField in
include K in
theorem isNonarchLocalField_of_finiteDimensional :
    ‚àÉ (_ : ValuativeRel L) (_ : ValuativeExtension K L)
    (_ : UniformSpace L), IsNonarchLocalField L := by
  letI : NontriviallyNormedField L := spectralNorm.nontriviallyNormedField K L
  haveI : IsUltrametricDist L := IsUltrametricDist.isUltrametricDist_of_isNonarchimedean_nnnorm
    isNonarchimedean_spectralNorm
  let v := NormedField.valuation (K := L)
  haveI := locallyCompactSpace_of_complete_of_finiteDimensional K L
  refine ‚ü®inferInstance, ‚ü®fun k‚ÇÅ k‚ÇÇ ‚Ü¶ ?_‚ü©, inferInstance, .mk‚ü©
  rw [Valuation.Compatible.rel_iff_le (v := v),
    Valuation.Compatible.rel_iff_le (v := ValuativeRel.valuation K)]
  change spectralNorm K L _ ‚â§ spectralNorm K L _ ‚Üî _
  rw [spectralNorm_extends, spectralNorm_extends]
  change Valued.norm _ ‚â§ Valued.norm _ ‚Üî _
  rw [Valued.norm_def, Valued.norm_def, NNReal.coe_le_coe,
    (Valuation.RankOne.strictMono Valued.v).le_iff_le]
  rfl

/- TODO:
1. Show that given a valuative extension, we can already make a local field (generalise the above
   proof)
2. Show that given an extension of local fields, the valuative rel is the same as this one given by
   the spectral norm.
3. As a result, conclude that there is only one valuative rel that is a valuative extension in the
   situation above.
-/

end make_finite_extension

end IsNonarchLocalField
