import Mathlib
import ClassFieldTheory.GroupCohomology._4_TateCohomology_def
import ClassFieldTheory.GroupCohomology._7_coind1_and_ind1

/-!
We define functors `up` and `down` from `Rep R G` to itself.
`up.obj M` is defined to be the cokernel of the injection `coindâ‚'_Î¹ : M âŸ¶ coindâ‚'.obj M` and
`down.obj M` is defined to be the kernel of the surjection `indâ‚'_Ï€ : indâ‚'.obj M â†’ M`.
Hence for any `M : Rep R G` we construct two short exact sequences
(the second defined only for finite `G`):

  `0 âŸ¶ M âŸ¶ coindâ‚'.obj M âŸ¶ up.obj M âŸ¶ 0` and
  `0 âŸ¶ down.obj M âŸ¶ coindâ‚'.obj M âŸ¶ M âŸ¶ 0`.

These can be used for dimension-shifting because `coindâ‚'.obj M` has trivial cohomology and
`indâ‚'.obj M` has trivial homology. I.e. for all `n > 0` we have (for every subgroup `S` of `G`):

  `Hâ¿(S,up M) â‰… Hâ¿âºÂ¹(S,M)` and
  `Hâ‚™(S,down M) â‰… Hâ‚™â‚Šâ‚(S,M)`.

If `G` is finite then both the induced and the
coinduced representations have trivial Tate cohomology,
so we have:

  `Hâ¿_{Tate}(S, up M) â‰… Hâ¿âºÂ¹_{Tate}(S,M)`,
  `Hâ¿_{Tate}(S, down M) â‰… Hâ¿â»Â¹_{Tate}(S,M)`.

-/

open
  Rep
  Representation
  CategoryTheory
  NatTrans
  ConcreteCategory
  Limits
  groupCohomology

noncomputable section

variable {R G : Type} [CommRing R] [Group G] (M : Rep R G)

namespace Rep.dimensionShift

@[simp] lemma forgetâ‚‚_map_apply {N : Rep R G} (f : M âŸ¶ N) :
    (forgetâ‚‚ (Rep R G) (ModuleCat R)).map f = f.hom := rfl

lemma coindâ‚'_Î¹.app_apply {M : Rep R G} (m : M) (x : G) : (coindâ‚'_Î¹.app M m) x = m := rfl

/--
The map from `M` to its coinduced representation is a monomorphism.
-/
instance : Mono (coindâ‚'_Î¹.app M) where
  right_cancellation g f hgf := by
    ext n
    have : Function.Injective (hom (coindâ‚'_Î¹.app M)) := by
      refine (injective_iff_map_eq_zero' (hom (coindâ‚'_Î¹.app M))).mpr (fun a  => ?_)
      constructor
      Â· intro g
        have : Function.const G a = 0 := by exact g
        simpa [Function.const_eq_zero] using this
      Â· exact fun a_1 â†¦ congrArg (â‡‘(hom (coindâ‚'_Î¹.app M))) a_1
    exact this (congrFun (congrArg DFunLike.coe (congrArg hom hgf)) n)

/--
The functor taking `M : Rep R G` to `up.obj M`, defined by the short exact sequence

  `0 âŸ¶ M âŸ¶ coindâ‚'.obj M âŸ¶ up.obj M âŸ¶ 0`.

Since `coindâ‚'.obj M` is acyclic, the cohomology of `up.obj M` is a shift by one
of the cohomology of `M`.
-/
@[simps] def up : Rep R G â¥¤ Rep R G where
  obj M := cokernel (coindâ‚'_Î¹.app M)
  map f:= by
    apply cokernel.desc _ (coindâ‚'.map f â‰« cokernel.Ï€ _)
    rw [â†Category.assoc, â†coindâ‚'_Î¹.naturality, Category.assoc, cokernel.condition, comp_zero]
  map_id := by simp
  map_comp f g := by simpa only using coequalizer.hom_ext (by simp)

/--
The functor taking `M : Rep R G` to the short complex:

  `M âŸ¶ coindâ‚'.obj M âŸ¶ up.obj M`.

-/
@[simps] def upShortComplex : Rep R G â¥¤ ShortComplex (Rep R G) where
  obj M := {
    Xâ‚ := M
    Xâ‚‚ := coindâ‚'.obj M
    Xâ‚ƒ := up.obj M
    f := coindâ‚'_Î¹.app M
    g := cokernel.Ï€ (coindâ‚'_Î¹.app M)
    zero := cokernel.condition (coindâ‚'_Î¹.app M)
  }
  map f := {
    Ï„â‚ := f
    Ï„â‚‚ := coindâ‚'.map f
    Ï„â‚ƒ := up.map f
    commâ‚â‚‚ := coindâ‚'_Î¹.naturality f
    commâ‚‚â‚ƒ := (cokernel.Ï€_desc _ _ _).symm
  }
  map_comp f g := by
    congr
    rw [Functor.map_comp]
  map_id M := by
    congr
    rw [up_map]
    apply IsColimit.desc_self

/--
`upShortComplex.obj M` is a short exact sequence of representations.
-/
lemma up_shortExact : (upShortComplex.obj M).ShortExact where
  exact := ShortComplex.exact_cokernel (coindâ‚'_Î¹.app M)
  mono_f := inferInstanceAs (Mono (coindâ‚'_Î¹.app M))
  epi_g := coequalizer.Ï€_epi

lemma up_shortExact_res {H : Type} [Group H] [DecidableEq G] (Ï† : H â†’* G) :
    ((upShortComplex.obj M).map (res Ï†)).ShortExact := by
  rw [res_respectsShortExact]
  exact up_shortExact M

abbrev up_Ï€ : coindâ‚' âŸ¶ up (R := R) (G := G) where
  app _             := (upShortComplex.obj _).g
  naturality _ _ _  := (upShortComplex.map _).commâ‚‚â‚ƒ

variable [DecidableEq G]
/--
The connecting homomorphism from `Hâ°(G,up M)` to `HÂ¹(G,M)` is
an epimorphism (i.e. surjective).
-/
instance up_Î´_zero_epi : Epi (Î´ (up_shortExact M) 0 1 rfl) := by
  refine epi_Î´_of_isZero (up_shortExact M) 0 ?_
  simpa only [upShortComplex_obj_Xâ‚‚, zero_add] using isZero_of_trivialCohomology

/--
The connecting homomorphism from `Hâ¿âºÂ¹(G,up M)` to `Hâ¿âºÂ²(G,M)` is an isomorphism.
-/
instance up_Î´_isIso (n : â„•) : IsIso (Î´ (up_shortExact M) (n + 1) (n + 2) rfl) := by
  refine isIso_Î´_of_isZero (up_shortExact M) (n + 1) ?_ ?_
  all_goals simpa only [upShortComplex_obj_Xâ‚‚] using isZero_of_trivialCohomology

def up_Î´iso (n : â„•) : groupCohomology (up.obj M) (n + 1) â‰… groupCohomology M (n + 2) :=
  asIso (Î´ (up_shortExact M) (n + 1) (n + 2) rfl)

def up_Î´iso_natTrans (n : â„•) : up â‹™ functor R G (n + 1) â‰… functor R G (n + 2) :=
  NatIso.ofComponents (fun X => by simpa [Functor.comp_obj, functor_obj] using up_Î´iso (M := X) n)
  <| fun {X Y} f â†¦ by
      refine id (Eq.symm (HomologicalComplex.HomologySequence.Î´_naturality
        (ShortComplex.homMk ((cochainsFunctor R G).map (upShortComplex.map f).1)
        ((cochainsFunctor R G).map (upShortComplex.map f).2) ((cochainsFunctor R G).map (upShortComplex.map f).3)
        rfl (?_)) (map_cochainsFunctor_shortExact (up_shortExact X))
        (map_cochainsFunctor_shortExact (up_shortExact Y)) (n+1) (n+2) rfl))
      simp only [ShortComplex.map_Xâ‚‚, upShortComplex_obj_Xâ‚‚, cochainsFunctor_obj, ShortComplex.map_Xâ‚ƒ,
        upShortComplex_obj_Xâ‚ƒ, up_obj, Functor.id_obj, upShortComplex_map_Ï„â‚‚, cochainsFunctor_map, ShortComplex.map_g,
        upShortComplex_obj_g, upShortComplex_map_Ï„â‚ƒ, up_map]
      have : coindâ‚'.map f â‰« cokernel.Ï€ (coindâ‚'_Î¹.app Y) = cokernel.Ï€ (coindâ‚'_Î¹.app X) â‰«
        cokernel.desc (coindâ‚'_Î¹.app X) ((coindâ‚'.map f) â‰« cokernel.Ï€ (coindâ‚'_Î¹.app Y))
        (up._proof_2 f) :=(cokernel.Ï€_desc _ _ _).symm
      ext a b c
      simp only [CochainComplex.of_x, HomologicalComplex.comp_f, ModuleCat.hom_comp,
        cochainsMap_id_f_hom_eq_compLeft, LinearMap.coe_comp, Function.comp_apply,
        LinearMap.compLeft_apply]
      calc
       _ = (hom (coindâ‚'.map f â‰« cokernel.Ï€ (coindâ‚'_Î¹.app Y))) (b c) := rfl
       _ = (hom (cokernel.Ï€ (coindâ‚'_Î¹.app X) â‰« cokernel.desc (coindâ‚'_Î¹.app X)
        (coindâ‚'.map f â‰« cokernel.Ï€ (coindâ‚'_Î¹.app Y)) (up._proof_2 f)))
        (b c):= by rw [congrFun (congrArg DFunLike.coe (congrArg hom this)) (b c)]
       _ = _ := rfl

/--
The connecting homomorphism from `H^{n+1}(G,up M)` to `H^{n+2}(G,M)` is
an epimorphism (i.e. surjective).
-/
instance up_Î´_zero_epi_res {S : Type} [Group S] [DecidableEq S] {Ï† : S â†’* G}
    (inj : Function.Injective Ï†) : Epi (Î´ (up_shortExact_res M Ï†) 0 1 rfl) := by
  refine epi_Î´_of_isZero (up_shortExact_res M Ï†) 0 ?_
  simpa only [ShortComplex.map_Xâ‚‚, upShortComplex_obj_Xâ‚‚, zero_add] using TrivialCohomology.isZero Ï† inj

/--
The connecting homomorphism from `H^{n+1}(G,up M)` to `H^{n+2}(G,M)` is an
isomorphism.
-/
instance up_Î´_isIso_res {S : Type} [Group S] [DecidableEq S] {Ï† : S â†’* G}
    (inj : Function.Injective Ï†) (n : â„•) :
    IsIso (Î´ (up_shortExact_res M Ï†) (n + 1) (n + 2) rfl) := by
  refine isIso_Î´_of_isZero (up_shortExact_res M Ï†) (n + 1) ?_ ?_
  all_goals simpa only [ShortComplex.map_Xâ‚‚, upShortComplex_obj_Xâ‚‚] using TrivialCohomology.isZero Ï† inj

def up_Î´iso_res {S : Type} [Group S] [DecidableEq S] {Ï† : S â†’* G}
    (inj : Function.Injective Ï†) (n : â„•) :
    groupCohomology (up.obj M â†“ Ï†) (n + 1) â‰… groupCohomology (M â†“ Ï†) (n + 2) := by
  have := up_Î´_isIso_res M inj n
  apply asIso (Î´ (up_shortExact_res M Ï†) (n + 1) (n + 2) rfl)

omit [DecidableEq G] in
lemma indâ‚'_obj_Ï : (indâ‚'.obj M).Ï = M.Ï.indâ‚' := rfl

omit [DecidableEq G] in
lemma indâ‚'_obj_Ï_apply (g : G) : (indâ‚'.obj M).Ï g = M.Ï.indâ‚' g := rfl

omit [DecidableEq G] in
lemma indâ‚'_Ï€.app_hom : (indâ‚'_Ï€.app M).hom = ofHom Representation.indâ‚'_Ï€ := rfl

omit [DecidableEq G] in
lemma indâ‚'_Ï€.app_apply (f : indâ‚'.obj M) :
    (indâ‚'_Ï€.app M) f = Finsupp.sum f (fun _ â†¦ LinearMap.id (R := R)) := rfl

def down : Rep R G â¥¤ Rep R G where
  obj M := kernel (indâ‚'_Ï€.app M)
  map Ï† := kernel.lift _ (kernel.Î¹ _ â‰« indâ‚'.map Ï†) (by
    rw [Category.assoc, indâ‚'_Ï€.naturality, â†Category.assoc, kernel.condition, zero_comp])
  map_id _ := by simp
  map_comp f g := by simpa only using equalizer.hom_ext (by simp)

abbrev down_ses : ShortComplex (Rep R G) where
  Xâ‚ := down.obj M
  Xâ‚‚ := indâ‚'.obj M
  Xâ‚ƒ := M
  f := kernel.Î¹ (indâ‚'_Ï€.app M)
  g := indâ‚'_Ï€.app M
  zero := kernel.condition (indâ‚'_Ï€.app M)

@[simps] def downShortComplex : Rep R G â¥¤ ShortComplex (Rep R G) where
  obj M := {
    Xâ‚ :=down.obj M
    Xâ‚‚ := indâ‚'.obj M
    Xâ‚ƒ := M
    f := kernel.Î¹ (indâ‚'_Ï€.app M)
    g := indâ‚'_Ï€.app M
    zero := kernel.condition (indâ‚'_Ï€.app M)
  }
  map {X Y} f := {
    Ï„â‚ :=down.map f
    Ï„â‚‚ := indâ‚'.map f
    Ï„â‚ƒ :=  f
    commâ‚â‚‚ :=by
     simp only [down, Functor.id_obj, kernel.lift_Î¹]
    commâ‚‚â‚ƒ :=by
      simp only [Functor.id_obj, naturality, Functor.id_map]
  }
  map_comp f g := by
    simp only [Functor.id_obj, Functor.map_comp]
    rfl
  map_id M := by
    simp only [Functor.id_obj, CategoryTheory.Functor.map_id]
    rfl

omit [DecidableEq G] in
lemma down_shortExact : (down_ses M).ShortExact where
  exact   := ShortComplex.exact_kernel (indâ‚'_Ï€.app M)
  mono_f  := inferInstance
  epi_g   := inferInstance

omit [DecidableEq G] in
lemma down_shortExact_res {H : Type} [Group H] (Ï† : H â†’* G) :
    ((down_ses M).map (res Ï†)).ShortExact := by
  rw [res_respectsShortExact]
  exact down_shortExact M

variable [Finite G]

/--
The connecting homomorphism `Hâ°(G,down.obj M) âŸ¶ HÂ¹(G, M)` is an epimorphism if `G` is finite.
-/
instance down_Î´_zero_epi : Epi (Î´ (down_shortExact M) 0 1 rfl) := by
  refine epi_Î´_of_isZero (down_shortExact M) 0 ?_
  simpa only [zero_add] using isZero_of_trivialCohomology

/--
The connecting homomorphism `Hâ°(H,down.obj M â†“ H) âŸ¶ HÂ¹(H, M â†“ H)` is an epimorphism if
`H` is a subgroup of a finite group `G`.
-/
instance down_Î´_zero_res_epi {S : Type} [Group S] [DecidableEq S] {Ï† : S â†’* G}
    (inj : Function.Injective Ï†) : Epi (Î´ (down_shortExact_res M Ï†) 0 1 rfl) := by
  refine epi_Î´_of_isZero (down_shortExact_res M Ï†) 0 ?_
  simpa only [ShortComplex.map_Xâ‚‚, zero_add] using TrivialCohomology.isZero Ï† inj

/--
The connecting homomorphism `Hâ¿âºÂ¹(G,down.obj M) âŸ¶ Hâ¿âºÂ²(G, M)` is an isomorphism
if `G` is finite.
-/
instance down_Î´_isIso  (n : â„•) : IsIso (Î´ (down_shortExact M) (n + 1) (n + 2) rfl) := by
  refine isIso_Î´_of_isZero (down_shortExact M) (n + 1) ?_ ?_
  all_goals exact isZero_of_trivialCohomology

def down_Î´iso (n : â„•) : groupCohomology M (n + 1) â‰… groupCohomology (down.obj M) (n + 2) :=
  asIso (Î´ (down_shortExact M) (n + 1) (n + 2) rfl)

def down_Î´iso_natTrans (n : â„•) : functor R G (n + 1) â‰… down â‹™ functor R G (n + 2) :=
  NatIso.ofComponents (fun M â†¦ by simp only [functor_obj, Functor.comp_obj]; exact down_Î´iso M _)
  <| fun {X Y} f â†¦ by
    refine id (Eq.symm (HomologicalComplex.HomologySequence.Î´_naturality
      (ShortComplex.homMk ((cochainsFunctor R G).map (downShortComplex.map f).1)
      ((cochainsFunctor R G).map (downShortComplex.map f).2) ((cochainsFunctor R G).map (downShortComplex.map f).3)
      ?_ ?_ ) ( map_cochainsFunctor_shortExact (down_shortExact X))
      (map_cochainsFunctor_shortExact (down_shortExact Y)) (n+1) (n+2) rfl))
    simp only [ShortComplex.map_Xâ‚, cochainsFunctor_obj, ShortComplex.map_Xâ‚‚, downShortComplex_obj_Xâ‚,
      downShortComplex_map_Ï„â‚, cochainsFunctor_map, ShortComplex.map_f, Functor.id_obj, downShortComplex_obj_Xâ‚‚,
      downShortComplex_map_Ï„â‚‚]
    ext a b c
    simp only [CochainComplex.of_x, HomologicalComplex.comp_f, ModuleCat.hom_comp,
      cochainsMap_id_f_hom_eq_compLeft, LinearMap.coe_comp, Function.comp_apply,
      LinearMap.compLeft_apply]
    have :(down.map f) â‰« kernel.Î¹ (indâ‚'_Ï€.app Y )= (kernel.Î¹ (indâ‚'_Ï€.app X)) â‰« indâ‚'.map f := by
      simp only [down, Functor.id_obj, kernel.lift_Î¹]
    calc
      _ = hom ((down.map f) â‰« kernel.Î¹ (indâ‚'_Ï€.app Y)) (b c) := rfl
      _ = hom ((kernel.Î¹ (indâ‚'_Ï€.app X)) â‰« indâ‚'.map f) (b c) := by rw [this] ; rfl
      _ = _ := rfl
    simp only [ShortComplex.map_Xâ‚‚, cochainsFunctor_obj, ShortComplex.map_Xâ‚ƒ, downShortComplex_obj_Xâ‚‚,
      downShortComplex_map_Ï„â‚‚, cochainsFunctor_map, ShortComplex.map_g, downShortComplex_obj_Xâ‚ƒ, downShortComplex_map_Ï„â‚ƒ]
    ext a b c
    simp only [CochainComplex.of_x, HomologicalComplex.comp_f, ModuleCat.hom_comp,
      cochainsMap_id_f_hom_eq_compLeft, LinearMap.coe_comp, Function.comp_apply,
      LinearMap.compLeft_apply]
    calc
      _ = (hom ((indâ‚'.map f) â‰« (indâ‚'_Ï€.app Y))) (b c) := rfl
      _ = (hom (indâ‚'_Ï€.app X â‰« (ðŸ­ (Rep R G)).map f)) (b c) := by
        rw [(indâ‚'_Ï€ (G:=G) (R:=R)).naturality f]
      _ = _ := rfl

instance down_Î´_res_isIso (n : â„•) {H : Type} [Group H] [DecidableEq H] {Ï† : H â†’* G}
    (inj : Function.Injective Ï†) : IsIso (Î´ (down_shortExact_res M Ï†) (n + 1) (n + 2) rfl) := by
  refine isIso_Î´_of_isZero (down_shortExact_res M Ï†) (n + 1) ?_ ?_
  all_goals simpa only [ShortComplex.map_Xâ‚‚] using TrivialCohomology.isZero Ï† inj

def down_Î´iso_res {H : Type} [Group H] [DecidableEq H] {Ï† : H â†’* G}
    (inj : Function.Injective Ï†) (n : â„•) :
    groupCohomology (M â†“ Ï†) (n + 1) â‰… groupCohomology (down.obj M â†“ Ï†) (n + 2) :=
  have := down_Î´_res_isIso M n inj
  asIso (Î´ (down_shortExact_res M Ï†) (n + 1) (n + 2) rfl)

end dimensionShift

end Rep

namespace groupCohomology

variable [Finite G]
open Rep
  dimensionShift

/--
An explicit version of `isZero_of_trivialtateCohomology`
-/
private lemma isZero_of_trivialtateCohomology' [DecidableEq G] (M : Rep R G)
    [M.TrivialtateCohomology] (n : â„¤) : IsZero ((tateComplexFunctor.obj M).homology n) :=
  TrivialtateCohomology.isZero (.id G) Function.injective_id

instance instIsIso_up_shortExact (M : Rep R G) [DecidableEq G] (n : â„¤) :
    IsIso (tateCohomology.Î´ (up_shortExact M) n) := by
  have _ : TrivialtateCohomology (coindâ‚'.obj M) := inferInstance
  refine ShortComplex.ShortExact.isIso_Î´
    (tateCohomology.map_tateComplexFunctor_shortExact (up_shortExact M))
    n (n + 1) (by rfl) (by simp;exact isZero_of_trivialtateCohomology' (coindâ‚'.obj M) n)
    (by simp;exact isZero_of_trivialtateCohomology' (coindâ‚'.obj M) (n + 1))

instance instIsIso_down_shortExact (M : Rep R G) [DecidableEq G] (n : â„¤) :
    IsIso (tateCohomology.Î´ (down_shortExact M) n) := by
  have _ : TrivialtateCohomology (indâ‚'.obj M) := inferInstance
  refine ShortComplex.ShortExact.isIso_Î´
    (tateCohomology.map_tateComplexFunctor_shortExact (down_shortExact M))
    n (n + 1) (by rfl) (by simp;exact isZero_of_trivialtateCohomology' (indâ‚'.obj M) n)
    (by simp;exact isZero_of_trivialtateCohomology' (indâ‚'.obj M) (n + 1))

def upÎ´iso_Tate (n : â„¤) [DecidableEq G] (M : Rep R G) :
    (tateCohomology n).obj (up.obj M) â‰… (tateCohomology (n + 1)).obj M :=

  have := instIsIso_up_shortExact M n
  asIso (tateCohomology.Î´ (up_shortExact M) n)

def downÎ´iso_Tate (n : â„¤) [DecidableEq G] (M : Rep R G) :
    (tateCohomology n).obj M â‰… (tateCohomology (n + 1)).obj (down.obj M) :=
  asIso (tateCohomology.Î´ (down_shortExact M) n)

end groupCohomology

end
