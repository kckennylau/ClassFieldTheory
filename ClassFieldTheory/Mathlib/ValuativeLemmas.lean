import Mathlib.Analysis.Normed.Unbundled.SpectralNorm
import Mathlib.RingTheory.Valuation.ValuativeRel
import Mathlib.Topology.Algebra.Valued.NormedValued

universe u uâ‚€ v v'

variable {R : Type u} [CommRing R] [ValuativeRel R]
  {F : Type uâ‚€} [Field F] [ValuativeRel F]
  {Î“ : Type v} [LinearOrderedCommMonoidWithZero Î“]
  {Î“' : Type v'} [LinearOrderedCommMonoidWithZero Î“']
  (v : Valuation R Î“) [Valuation.Compatible v] (vâ‚€ : Valuation F Î“) [Valuation.Compatible vâ‚€]
  (x y : R) (xâ‚€ yâ‚€ : F)


namespace Valuation

/-- The "valuation ball" is a valuation version of the open balls centered at 0 in a metric
topology. This is used in `ValuativeTopology` for the statement that a valuative relation is
compatible with a given topology -/
def ball (v : Valuation R Î“) (Î³ : Î“) : Set R :=
  { x | v x < Î³ }

omit [ValuativeRel R] in
@[simp] lemma mem_ball_iff (x : R) (Î³ : Î“) :
    x âˆˆ v.ball Î³ â†” v x < Î³ := by
  rw [ball, Set.mem_setOf_eq]

namespace Compatible

lemma rel_one_iff :
    x â‰¤áµ¥ 1 â†” v x â‰¤ 1 := by
  rw [â† map_one v, â† Valuation.Compatible.rel_iff_le]

lemma rel_zero_iff :
    x â‰¤áµ¥ 0 â†” v x â‰¤ 0 := by
  rw [â† map_zero v, â† Valuation.Compatible.rel_iff_le]

lemma one_rel_iff :
    1 â‰¤áµ¥ x â†” 1 â‰¤ v x := by
  rw [â† map_one v, â† Valuation.Compatible.rel_iff_le]

section compare

-- `v` is placed first here because that's the valuation one would typically want to rewrite into.
variable (v' : Valuation R Î“') [Valuation.Compatible v']

lemma lt_iff_lt (x y : R) : v' x < v' y â†” v x < v y := by
  simp_rw [lt_iff_le_not_ge, â† Valuation.Compatible.rel_iff_le]

lemma ball_eq_ball (x : R) :
    v'.ball (v' x) = v.ball (v x) := by
  ext y; simp_rw [mem_ball_iff, Valuation.Compatible.lt_iff_lt v v']

lemma lt_one_iff_lt_one (x : R) : v' x < 1 â†” v x < 1 := by
  rw [â† v.map_one, â† v'.map_one, lt_iff_lt v v']

lemma one_lt_iff_one_lt (x : R) : 1 < v' x â†” 1 < v x := by
  rw [â† v.map_one, â† v'.map_one, lt_iff_lt v v']

end compare

end Valuation.Compatible


namespace ValuativeRel

@[ext] theorem ext {R : Type u} [CommRing R] {v v' : ValuativeRel R}
    (h : âˆ€ a b, v.rel a b â†” v'.rel a b) : v = v' := by
  cases v; cases v'; congr; ext; apply h

theorem rel_iff_div_rel_one {F : Type u} [Field F] [ValuativeRel F]
    (x : F) {y : F} (hy : y â‰  0) : x â‰¤áµ¥ y â†” x / y â‰¤áµ¥ 1 := by
  rw [Valuation.Compatible.rel_iff_le (v := ValuativeRel.valuation F),
    Valuation.Compatible.rel_iff_le (v := ValuativeRel.valuation F),
    map_divâ‚€, map_one, div_le_oneâ‚€ (bot_lt_iff_ne_bot.2 ((map_ne_zero _).2 hy))]

theorem rel_zero_iff {F : Type u} [Field F] [ValuativeRel F] (x : F) :
    x â‰¤áµ¥ 0 â†” x = 0 := by
  rw [Valuation.Compatible.rel_iff_le (v := valuation F), map_zero, le_zero_iff, map_eq_zero]

/-- Two valuative relations on a field are equal iff their rings of integers are equal. -/
@[ext high] theorem ext_field {F : Type u} [Field F] {v v' : ValuativeRel F}
    (h : âˆ€ x, v.rel x 1 â†” v'.rel x 1) : v = v' := by
  ext x y
  by_cases hy : y = 0
  Â· simp_rw [hy, rel_zero_iff]
  Â· rw [rel_iff_div_rel_one _ hy, @rel_iff_div_rel_one _ _ v x y hy, h]

theorem posSubmonoid.ne_zero {R : Type u} [CommRing R] [ValuativeRel R]
    (x : posSubmonoid R) : x.val â‰  0 :=
  mt (Â· â–¸ rel_rfl) x.2

theorem valuation_surjectiveâ‚€ {F : Type u} [Field F] [ValuativeRel F]
    (Î³ : ValueGroupWithZero F) : âˆƒ x : F, valuation F x = Î³ :=
  let âŸ¨x, y, hxyâŸ© := valuation_surjective Î³
  âŸ¨x / y.val, by rw [map_divâ‚€, hxy]âŸ©

theorem units_map_valuation_surjective {F : Type u} [Field F] [ValuativeRel F]
    (Î³ : (ValueGroupWithZero F)Ë£) : âˆƒ x : FË£, Units.map (valuation F) x = Î³ :=
  let âŸ¨x, hxâŸ© := valuation_surjectiveâ‚€ Î³.val
  âŸ¨Units.mk0 x (mt (by rw [â† hx, Â·, map_zero]) Î³.ne_zero),
    Units.ext <| by simpa using hxâŸ©


end ValuativeRel


namespace ValuativeTopology

open Topology ValuativeRel

theorem mk_replace {R : Type u} [CommRing R] [ValuativeRel R] [TopologicalSpace R]
    (h : âˆ€ s : Set R, s âˆˆ ð“ (0 : R) â†” âˆƒ Î³ : (ValueGroupWithZero R)Ë£, (valuation R).ball Î³ âŠ† s) :
    ValuativeTopology R where
  mem_nhds_iff := h

theorem mk' {F : Type u} [Field F] [ValuativeRel F] [TopologicalSpace F]
    {Î“â‚€ : Type v} [LinearOrderedCommMonoidWithZero Î“â‚€] (v : Valuation F Î“â‚€) [v.Compatible]
    (h : âˆ€ s : Set F, s âˆˆ ð“ (0 : F) â†” âˆƒ x : F, x â‰  0 âˆ§ v.ball (v x) âŠ† s) :
    ValuativeTopology F where
  mem_nhds_iff s := by
    rw [h s, Function.Surjective.exists (units_map_valuation_surjective)]
    simp_rw [Valuation.Compatible.ball_eq_ball (valuation F)]
    exact âŸ¨fun âŸ¨x, hx0, hxâŸ© â†¦ âŸ¨Units.mk0 x hx0, hxâŸ©, fun âŸ¨x, hxâŸ© â†¦ âŸ¨x.val, x.ne_zero, hxâŸ©âŸ©

end ValuativeTopology


namespace NormedField

variable {K : Type u} [NormedField K] [IsUltrametricDist K]

variable (K) in
def toValuativeRel : ValuativeRel K :=
  .ofValuation valuation
scoped [NormedField] attribute [instance] NormedField.toValuativeRel

theorem compatible : valuation.Compatible (R := K) where
  rel_iff_le _ _ := Iff.rfl
scoped [NormedField] attribute [instance] NormedField.compatible

@[simp] theorem ball_norm_eq {K : Type u} [NormedField K] [IsUltrametricDist K] (x : K) :
    Metric.ball 0 â€–xâ€– = { y : K | valuation y < valuation x } := by
  ext y
  simp_rw [mem_ball_zero_iff, Set.mem_setOf_eq, Valuation.Compatible.lt_iff_lt valuation,
    valuation_apply, â† NNReal.coe_lt_coe, coe_nnnorm]

theorem valuation_ball_eq (x : K) :
    (valuation (K := K)).ball (valuation x) = Metric.ball 0 â€–xâ€– := by
  ext; simp_rw [Valuation.mem_ball_iff, valuation_apply, â† NNReal.coe_lt_coe,
    coe_nnnorm, mem_ball_zero_iff]

variable (K) in
omit [IsUltrametricDist K] in
lemma trivial_or_non_trivial : (âˆ€ x : K, x = 0 âˆ¨ â€–xâ€– = 1) âˆ¨ (âˆƒ x : K, 1 < â€–xâ€–) := by
  by_contra h
  push_neg at h
  obtain âŸ¨âŸ¨x, hx0, hx1âŸ©, hkâŸ© := h
  obtain hx1 | h1x := lt_or_gt_of_ne hx1
  Â· exact absurd (hk xâ»Â¹) (not_le_of_gt <| by rwa [norm_inv, one_lt_invâ‚€ (norm_pos_iff.2 hx0)])
  Â· exact not_lt_of_ge (hk x) h1x

theorem nhds_zero_basis_norm {K : Type u} [NormedField K] :
    (nhds 0).HasBasis (fun x : K â†¦ 0 < â€–xâ€–) fun x â†¦ Metric.ball (0 : K) â€–xâ€– where
  mem_iff' s := by
    refine âŸ¨fun hs0 â†¦ ?_, fun âŸ¨x, x_pos, hxsâŸ© â†¦ ?_âŸ©
    Â· obtain trivial | âŸ¨x, h1xâŸ© := trivial_or_non_trivial K
      Â· refine âŸ¨1, by simp, fun y hy1 â†¦ ?_âŸ©
        rw [(trivial y).resolve_right (ne_of_lt (by simpa using hy1))]
        exact mem_of_mem_nhds hs0
      Â· have hix1 : â€–xâ»Â¹â€– < 1 := norm_inv x â–¸ inv_lt_one_of_one_ltâ‚€ h1x
        have ix_pos : 0 < â€–xâ»Â¹â€– := norm_inv x â–¸ inv_pos_of_pos (one_pos.trans h1x)
        rw [(Metric.nhds_basis_ball_pow ix_pos hix1).mem_iff] at hs0
        obtain âŸ¨n, -, hnsâŸ© := hs0
        refine âŸ¨(xâ»Â¹) ^ n, norm_pow xâ»Â¹ n â–¸ pow_pos ix_pos n, by rwa [norm_pow]âŸ©
    Â· exact Metric.nhds_basis_ball.mem_iff.2 âŸ¨_, x_pos, fun y hy â†¦ hxs (by simpa using hy)âŸ©

theorem _root_.DiscreteTopology.of_trivial_norm (trivial : âˆ€ x : K, x = 0 âˆ¨ â€–xâ€– = 1) :
    DiscreteTopology K :=
  DiscreteTopology.of_forall_le_norm one_pos fun x hx â†¦ by rw [(trivial x).resolve_left hx]

theorem valuativeTopology (K : Type u) [NormedField K] [IsUltrametricDist K] :
    ValuativeTopology K :=
  .mk' valuation fun s â†¦ by simp_rw [valuation_ball_eq, nhds_zero_basis_norm.mem_iff, norm_pos_iff]

theorem isNontrivial (K : Type u) [NontriviallyNormedField K] [IsUltrametricDist K] :
    ValuativeRel.IsNontrivial K := by
  obtain âŸ¨x, hxâŸ© := NontriviallyNormedField.non_trivial (Î± := K)
  refine âŸ¨âŸ¨ValuativeRel.valuation K x, ?_, ?_âŸ©âŸ©
  Â· rw [Valuation.ne_zero_iff]
    exact norm_pos_iff.1 (one_pos.trans hx)
  Â· refine ne_of_gt ?_
    rwa [Valuation.Compatible.one_lt_iff_one_lt valuation, valuation_apply,
      â† NNReal.coe_lt_coe, NNReal.coe_one, coe_nnnorm]

scoped [NormedField] attribute [instance] valuativeTopology isNontrivial

end NormedField


theorem locallyCompactSpace_of_complete_of_finiteDimensional (K : Type u) (L : Type v)
    [NontriviallyNormedField K] [CompleteSpace K] [LocallyCompactSpace K]
    [AddCommGroup L] [TopologicalSpace L] [IsTopologicalAddGroup L] [T2Space L]
    [Module K L] [ContinuousSMul K L] [FiniteDimensional K L] :
    LocallyCompactSpace L := by
  obtain âŸ¨s, âŸ¨bâŸ©âŸ© := Basis.exists_basis K L
  haveI := FiniteDimensional.fintypeBasisIndex b
  exact b.equivFun.toContinuousLinearEquiv.toHomeomorph.isOpenEmbedding.locallyCompactSpace


noncomputable
def spectralNorm.nontriviallyNormedField (K : Type u) [NontriviallyNormedField K] (L : Type v)
    [Field L] [Algebra K L] [Algebra.IsAlgebraic K L] [IsUltrametricDist K] [CompleteSpace K] :
    NontriviallyNormedField L where
  __ := spectralNorm.normedField K L
  non_trivial :=
    let âŸ¨x, hxâŸ© := NontriviallyNormedField.non_trivial (Î± := K)
    âŸ¨algebraMap K L x, hx.trans_eq <| (spectralNorm_extends _).symmâŸ©
